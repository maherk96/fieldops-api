plugins {
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'com.diffplug.spotless' version '6.25.0'
    id "org.owasp.dependencycheck" version "9.0.9"
    id 'jacoco'
}

group = 'com.fieldops'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

bootRun {
    environment "SPRING_PROFILES_ACTIVE", (System.getenv("SPRING_PROFILES_ACTIVE") ?: "local")
}

dependencyCheck {
    failBuildOnCVSS = 7.0
    analyzers {
        assemblyEnabled = false
    }
}

dependencies {
    // --- Spring Boot ---
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // --- JWT ---
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // --- Database ---
    runtimeOnly 'org.postgresql:postgresql'

    // --- Lombok ---
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // --- Tests ---
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'io.qameta.allure:allure-junit5:2.28.0'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport

    systemProperty "allure.results.directory", "${buildDir}/allure-results"

    systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.70
            }
        }
    }
}

tasks.named('check') {
    dependsOn tasks.named('jacocoTestCoverageVerification')
}

spotless {
    java {
        googleJavaFormat('1.19.2')
        target 'src/*/java/**/*.java'
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

tasks.named('build') {
    dependsOn tasks.named('spotlessCheck')
}